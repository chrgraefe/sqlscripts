USE MASTER
GO

/* Audit Table */
IF OBJECT_ID('dbo.T_SERVERLOGONHISTORY') IS NOT NULL
	DROP TABLE [dbo].[T_SERVERLOGONHISTORY]
GO

CREATE TABLE [dbo].[T_SERVERLOGONHISTORY](
	[SATZ_ID] [int] IDENTITY(1,1) NOT NULL,
	[SYSTEMUSER] [nvarchar](512) NULL,
	[DBUSER] [nvarchar](512) NULL,
	[APPLICATION] [nvarchar](512) NULL,
	[HOSTNAME] [nvarchar](512) NULL,
	[SPID] [int] NULL,
	[LOGONTIME] [datetime] NULL,
 CONSTRAINT [PK_T_SERVERLOGONHISTORY] PRIMARY KEY CLUSTERED ([SATZ_ID] ASC)
) 
GO

IF OBJECT_ID('dbo.usp_InsertLogon') IS NOT NULL
	DROP PROCEDURE dbo.usp_InsertLogon
GO
CREATE PROCEDURE dbo.usp_InsertLogon

	 @SYSTEM_USER SYSNAME
	,@USER SYSNAME
	,@APPLICATION NVARCHAR(128)
	,@HOSTNAME NVARCHAR(128)
	,@SPID SMALLINT
	,@TIMESTAMP DATETIME
WITH EXECUTE AS OWNER
AS
BEGIN

	SET NOCOUNT ON;

	INSERT INTO master.dbo.T_SERVERLOGONHISTORY
	(
	 SYSTEMUSER
	,DBUSER
	,[APPLICATION]
	,[HOSTNAME]
	,SPID
	,LOGONTIME
	)
	VALUES( 
	 @SYSTEM_USER
	,@USER
	,@APPLICATION
	,@HOSTNAME
	,@SPID
	,@TIMESTAMP
	)
	;
	
END

GO
/* Create Logon Trigger */
ALTER TRIGGER TRG_SERVERLOGON
ON ALL SERVER FOR LOGON
AS
BEGIN

	SET NOCOUNT ON;

	DECLARE 
		 @SYSTEM_USER SYSNAME = SYSTEM_USER
		,@USER SYSNAME = USER
		,@APPLICATION NVARCHAR(128) = APP_NAME()
		,@HOSTNAME NVARCHAR(128) = HOST_NAME()
		,@SPID SMALLINT = @@SPID
		,@TIMESTAMP DATETIME = GETDATE()	

	EXECUTE master.dbo.usp_InsertLogon
	 @SYSTEM_USER = @SYSTEM_USER
	,@USER = @USER
	,@APPLICATION = @APPLICATION
	,@HOSTNAME = @HOSTNAME
	,@SPID = @SPID
	,@TIMESTAMP = @TIMESTAMP
	;
	
END
GO


--GRANT ALL ON OBJECT::dbo.T_SERVERLOGONHISTORY TO public;



/*
DISABLE TRIGGER TRG_SERVERLOGON ON ALL SERVER
GO
ENABLE TRIGGER TRG_SERVERLOGON ON ALL SERVER
GO
*/